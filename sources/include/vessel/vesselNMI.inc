; vesselnmi.inc 2021 m64

; NMI code

vessel_NMI_ON
        lda #$7f
        sta $dc0d  ;disable timer interrupts which can be generated by the two CIA chips
        lda #$80
        sta $dd0d  ;
        lda $dc0d  ;by reading this two registers we negate any pending CIA irqs.
        lda $dd0d  ;if we don't do this, a pending CIA irq might occur after we finish setting up our irq.
        
        ; configure VESSEL for NMI: ON 1001
        ;Set PA2 to 1 to signal OUTPUT
        lda $dd00
        ora #%00000100  ;Set bit2 to 1
        sta $dd00

        ;Set Port B to output
        lda #$ff
        sta $dd03

        ;Send command 0x4, enable NMI.
        lda #$fd
        sta $dd01
        lda #$04
        sta $dd01
        lda #%00001001 ; Vessel CFG Featues: NMI + NMI status only
        sta $dd01

        ; NMI usually points to label 'NMI' (0aea), no need to save old vector

        lda #<VESSELNMI
        ldy #>VESSELNMI
        jsr MIDIC64.setNMI

        lda #$90 ;%10010000 ; /FLAG only
        sta $dd0d
           
        rts

vessel_NMI_OFF
        ; disable playback in NMI handler

        lda #$7f
        sta $dc0d  ;disable timer interrupts which can be generated by the two CIA chips
        ; sta $dd0d  ;

        lda $dc0d  
        lda $dd0d  

        ; configure VESSEL for NMI: OFF 
        ; Set PA2 to 1 to signal OUTPUT
        lda $dd00
        ora #%00000000  ;Set bit2 to 1
        sta $dd00

        ; Set Port B to output
        lda #$ff
        sta $dd03

        ; Send command 0x4, disable NMI.
        lda #$fd
        sta $dd01
        lda #$04
        sta $dd01
        lda #%00000000
        sta $dd01

        ; restore NMI
        lda #<NMI
        ldy #>NMI
        jsr MIDIC64.setNMI  

        rts

        ; NMI PLAY ROUTINE (on NMI)
VESSELNMI
        pha             ;store sensitive registers and bank status
        lda banksel
        pha
        txa             ;faster entry from MIDI-C64
        pha
        tya
        pha

        lda #$35    
        sta banksel

        bit $dd0d
        
        jsr NMIReadMIDI
        cpy #MIDI.TimingClock
        bne _exitNMI
                
        lda #1
        sta VESSELPLAYING
        jsr playadapter.playsubb
        jsr playadapter.followplay

        inc playadapter.seqrefr ;refresh also when playing changes SEQCNT,x
        inc display.insrefr+1
        jsr playadapter.padapter

_exitNMI
        pla
        tay
        pla
        tax
        pla             ;written at NMI entry, stored value of $01 ROM/RAM bank switcher
        sta banksel
        pla   

        rti  

NMI_MIDI_STOP  .byte 0
NMI_MIDI_START .byte 0

VESSEL_NMISTOP
        ; MIDI STOP: stop playback
        lda #0
        sta playadapter.playmod
        sta VESSELPLAYING
        sei
        jsr vessel_NMI_OFF

        lda #0
        sta $d020
        sta $d021
        sta NMI_MIDI_STOP
        sta VESSELMODE
        sta VESSEL_OVERLAY_PTN
        ldx #4 ; normal is purple
        stx plybcol
        stx plybcol+1 ; restore border
        stx $d020
        lda #0
        sta menu.m64btx

        jsr v_scrn_restore

        lda #$35
        sta banksel 
        jmp menu.retoedi 
        
        jmp goback
VESSELPLAYING .byte 0

; NMI MIDI READ Routine
NMIReadMIDI ; read without changing border color
        ; Reset PA2 to signal INPUT mode
        lda $dd00
        and #%11111011 ; Set bit2 to 0
        sta $dd00
        ; Set Port B to input
        lda #$00
        sta $dd03

        ; Read the available number of bytes. Max number of bytes in one go is 255 (not 256)
        ldy $dd01 ; Read bytecount from Port B
        beq _VesselEmpty
_RxByte lda $dd01 ; Read MIDI byte from Port B
        cmp #MIDI.StopSeqPlay
        bne +
        lda #1
        sta NMI_MIDI_STOP

+       cmp #MIDI.StartSqPlay
        bne +
        lda #1
        sta NMI_MIDI_START

+       dey
        bne _RxByte
_VesselEmpty
        tay
        ; Set PA2 to 1 to signal OUTPUT
        lda $dd00
        ora #%00000100
        sta $dd00
        ; Set Port B to output
        lda #$ff
        sta $dd03
        rts

; menu point NMI MODE handling
run_menupoint_syncmode        
        lda MIDIdev                     ; no MIDI no NMI     
        bne +
        jmp _menupoint_back             ; no MIDI no SYNC

+       lda #VMODE_NMI_SYNC
        sta VESSELMODE

_VNMI_PlayBeg 
        lda #$35
        sta banksel
        
        sei

        ldy #0
        sty NMI_MIDI_START
        sty IRQ.fspdctr+1 ;we must start with singleplayer after getting back to IRQ
        
        ; disable raster irq    
        lda $d01a
        and #$fe
        sta $d01a
        
        jsr vessel_NMI_ON
        cli
        ldy #0
        sty NMI_MIDI_STOP
        sty NMI_MIDI_START
        ldx #1
        stx playadapter.followp+1
        lda #1
        sta VESSEL_OVERLAY_PTN
        sta playadapter.playmod
        jsr playadapter.ptploff
        jsr playadapter.inisubb

        inc menu.clearmenu
        jsr menu.dispmenu
        jsr menu.menuback

        lda #1
        sta display.syncscr+1
        
        lda #DISP_SLOW_SKIP_SPEED
        sta DISP_SLOW_SKIP_CNT

        ; reset y scrl, 25rows
        ; lda $d011
        ; ora #%00001011
        ; sta $d011
        lda #$1b
        sta $D011

        lda #128+64+1 
        sta $d015 ; sprites ON

        ; spr y pos
        lda #60 + 1
        ; sta $d001; spr 0
        ; sta $d003 ; spr 1
        ; sta $d005 ; spr 2
        ; sta $d007 ; spr 3
        ; sta $d009 ; spr 4
        ; sta $d00b ; spr 5
        sta $d00d ; spr 6
        sta $d00f ; spr 7

        ; spr color
        lda #$0b
        sta $d02d
        lda #$04
        sta $d02e

        jsr v_scrn_save
        jsr clr_v_scrn
        jsr do_v_scrn

        ldx #0
        stx $d020
        ldx #1
        lda #2
             
_menup_sync_mode_ret
        stx plybcol
        stx plybcol+1 ; restore border
        sta menu.m64btx
_menupoint_back   
        jmp vessel
